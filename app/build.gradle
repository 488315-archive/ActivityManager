apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'

def keystorePropertiesFile = file("keystore/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  playAccountConfigs {
    defaultAccountConfig {
      jsonFile = file('key.json')
    }
  }

  compileSdkVersion app.compileSdk

  defaultConfig {
    applicationId "com.activitymanager"
    minSdkVersion 15
    targetSdkVersion 28
    versionCode 100
    versionName "1.0.0"

    multiDexEnabled true

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    playAccountConfig = playAccountConfigs.defaultAccountConfig

    vectorDrawables.useSupportLibrary = true

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }

  signingConfigs {
    release {
      keyAlias keystoreProperties['KEY_ALIAS']
      keyPassword keystoreProperties['KEY_PASSWORD']
      storeFile file(keystoreProperties['STORE_FILE'])
      storePassword keystoreProperties['STORE_PASSWORD']
    }
  }

  buildTypes {
    debug {
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      shrinkResources true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  play {
    track = 'internal'
    uploadImages = false
  }

  sourceSets {
    androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
  }

  lintOptions {
    baseline file("lint-baseline.xml")
  }
}

androidExtensions {
  experimental = true
}

dependencies {
  implementation "com.android.support:support-v4:$versions.support"
  implementation "com.android.support:appcompat-v7:$versions.support"
  implementation "com.android.support:preference-v7:$versions.support"
  implementation "com.android.support:support-vector-drawable:$versions.support"
  implementation "com.android.support:support-media-compat:$versions.support"
  implementation "com.android.support:customtabs:$versions.support"
  implementation "com.android.support:recyclerview-v7:$versions.support"
  implementation "com.android.support:cardview-v7:$versions.support"
  implementation "com.android.support:design:$versions.support"
  implementation "com.android.support.constraint:constraint-layout:$versions.constraintLayout"
  implementation "com.android.support:multidex:1.0.3"

  implementation "android.arch.paging:runtime:$versions.paging"

  implementation "android.arch.lifecycle:extensions:$versions.lifecycle"
  kapt "android.arch.lifecycle:compiler:$versions.lifecycle"

  implementation "android.arch.persistence.room:runtime:$versions.room"
  kapt "android.arch.persistence.room:compiler:$versions.room"

  implementation "com.github.bumptech.glide:glide:$versions.glide"
  kapt "com.github.bumptech.glide:compiler:$versions.glide"

  implementation "com.android.billingclient:billing:1.0"
  implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
  implementation "com.codemybrainsout.rating:ratingdialog:1.0.8"
  implementation "com.pddstudio:highlightjs-android:1.5.0"
  implementation "com.simplecityapps:recyclerview-fastscroll:1.0.18"
  implementation 'com.tomergoldst.android:tooltips:1.0.10'

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation "org.jetbrains.anko:anko-coroutines:$anko_version"
  implementation "androidx.core:core-ktx:0.3"

  testImplementation "junit:junit:4.12"
  testImplementation "android.arch.paging:common:$versions.paging"

  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test:rules:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
}
