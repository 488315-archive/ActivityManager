plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.activitymanager"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 416
        versionName "4.3.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    flavorDimensions "type"
    productFlavors {
        dev {
            dimension "type"
            applicationIdSuffix = ".dev"
        }

        prod {
            dimension "type"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
        dataBinding false
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    lint {
        abortOnError true
        baseline file('lint-baseline.xml')
        lintConfig file('lint.xml')
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation "androidx.activity:activity-ktx:1.6.0-rc02"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.browser:browser:1.4.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    def glide_version = "4.13.2"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.google.android.material:material:1.6.1"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation "com.maltaisn:icondialog:3.3.0"
    implementation "com.maltaisn:iconpack-community-material:5.3.45"
    implementation "com.simplecityapps:recyclerview-fastscroll:2.0.0"
    implementation "com.tomergoldst.android:tooltips:1.0.11"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    testImplementation "junit:junit:4.13.2"

    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.4.0"
}
