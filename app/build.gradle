apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = file("keystore/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  compileSdkVersion app.compileSdk

  defaultConfig {
    applicationId "com.activitymanager"
    minSdkVersion 16
    targetSdkVersion 28
    versionCode 316
    versionName "3.3.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    vectorDrawables.useSupportLibrary = true

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }

  flavorDimensions "type"
  productFlavors {
    dev {
      dimension "type"
      applicationIdSuffix = ".dev"
    }

    prod {
      dimension "type"
    }
  }

  signingConfigs {
    release {
      keyAlias keystoreProperties['KEY_ALIAS']
      keyPassword keystoreProperties['KEY_PASSWORD']
      storeFile file(keystoreProperties['STORE_FILE'])
      storePassword keystoreProperties['STORE_PASSWORD']
    }
  }

  buildTypes {
    debug {
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      shrinkResources true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
  }

  lintOptions {
    baseline file("lint-baseline.xml")
  }
}

androidExtensions {
  experimental = true
}

dependencies {
  implementation "androidx.appcompat:appcompat:1.1.0-alpha01"
  implementation "androidx.preference:preference:1.1.0-alpha02"
  implementation "androidx.vectordrawable:vectordrawable:1.1.0-alpha01"
  implementation "androidx.browser:browser:1.0.0"
  implementation "androidx.recyclerview:recyclerview:1.1.0-alpha01"
  implementation "androidx.cardview:cardview:1.0.0"
  implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
  implementation "androidx.paging:paging-runtime:$versions.paging"
  implementation "androidx.core:core-ktx:1.1.0-alpha03"
  implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
  kapt "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle"
  implementation "androidx.room:room-runtime:$versions.room"
  kapt "androidx.room:room-compiler:$versions.room"

  implementation "com.github.bumptech.glide:glide:$versions.glide"
  kapt "com.github.bumptech.glide:compiler:$versions.glide"

  implementation "com.google.android.material:material:1.1.0-alpha02"
  implementation "com.simplecityapps:recyclerview-fastscroll:1.0.20"
  implementation "com.tomergoldst.android:tooltips:1.0.11"

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation "org.jetbrains.anko:anko-coroutines:$anko_version"

  testImplementation "junit:junit:4.12"
  testImplementation "androidx.paging:paging-common:$versions.paging"

  androidTestImplementation "androidx.test:runner:1.1.1"
  androidTestImplementation "androidx.test:rules:1.1.1"
  androidTestImplementation "androidx.test.espresso:espresso-core:3.1.1"
  androidTestImplementation "androidx.test.espresso:espresso-intents:3.1.1"
}
